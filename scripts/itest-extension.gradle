configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

sourceSets {
    itest { sourceSet ->
        ["java", "groovy", "scala", "resources"].each {
            if (!sourceSet.hasProperty(it)) return
            sourceSet[it].srcDir file("src/itest/${it}")
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task itest(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath
    reports.html.destination = file("${reporting.baseDir}/itests")
}

task aggregateReports(type: TestReport) {
    destinationDir = file("${reporting.baseDir}/all-tests")
    reportOn project.test, project.itest
}
